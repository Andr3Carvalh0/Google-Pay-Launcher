plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
}

String GOOGLE_PAY_PACKAGE_ID = "com.google.android.apps.walletnfcrel"

String KEY = System.getenv("GOOGLE_PAY_LAUNCHER_KEY")
String PASSWORD = System.getenv("GOOGLE_PAY_LAUNCHER_PASSWORD")

android {
    compileSdk 30
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "pt.andre.googlepaylauncher"
        minSdk 23
        targetSdk 30
        versionCode 1
        versionName "1.0"
        manifestPlaceholders = [google_pay_id: GOOGLE_PAY_PACKAGE_ID]
        buildConfigField "String", "GOOGLE_PAY_PACKAGE_ID", "\"" + GOOGLE_PAY_PACKAGE_ID + "\""

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildFeatures {
        compose true
    }

    testOptions {
        reportDir = "$project.buildDir/testResults"
    }

    signingConfigs {
        release {
            storeFile rootProject.file('./secrets/release.jks')
            keyAlias KEY
            keyPassword PASSWORD
            storePassword PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.0.1'
    }

    packagingOptions {
        exclude "META-INF/licenses/**"
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.activity:activity-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'
    implementation 'androidx.activity:activity-compose:1.3.1'
    implementation 'androidx.compose.material:material:1.0.1'
    implementation 'androidx.compose.animation:animation:1.0.1'
    implementation 'androidx.compose.ui:ui-tooling:1.0.1'
    implementation 'com.google.dagger:hilt-android:2.38.1'

    kapt 'com.google.dagger:hilt-compiler:2.38.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:3.2.0'

    debugImplementation 'androidx.compose.ui:ui-test-manifest:1.0.1'
    androidTestImplementation 'androidx.compose.ui:ui-test:1.0.1'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.0.1'
}