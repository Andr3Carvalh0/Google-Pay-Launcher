plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
}

String GOOGLE_PAY_PACKAGE_ID = 'com.google.android.apps.walletnfcrel'

String KEY = System.getenv('GOOGLE_PAY_LAUNCHER_KEY')
String PASSWORD = System.getenv('GOOGLE_PAY_LAUNCHER_PASSWORD')

android {
    compileSdk 31

    defaultConfig {
        applicationId 'pt.andre.googlepaylauncher'
        minSdk 23
        targetSdk 31
        versionCode rootProject.ext.code()
        versionName rootProject.ext.name()
        manifestPlaceholders = [google_pay_id: GOOGLE_PAY_PACKAGE_ID]
        buildConfigField 'String', 'GOOGLE_PAY_PACKAGE_ID', "\"" + GOOGLE_PAY_PACKAGE_ID + "\""

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildFeatures {
        compose true
    }

    testOptions {
        reportDir = "$project.buildDir/testResults"
    }

    signingConfigs {
        release {
            storeFile rootProject.file('./secrets/release.jks')
            keyAlias KEY
            keyPassword PASSWORD
            storePassword PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix '.debug'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.0.5'
    }

    packagingOptions {
        exclude 'META-INF/licenses/**'
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation rootProject.ext.dependencies.activityCompose
    implementation rootProject.ext.dependencies.activityKtx
    implementation rootProject.ext.dependencies.appCompat
    implementation rootProject.ext.dependencies.composeAnimation
    implementation rootProject.ext.dependencies.composeMaterial
    implementation rootProject.ext.dependencies.composeUITooling
    implementation rootProject.ext.dependencies.coreKtx
    implementation rootProject.ext.dependencies.hilt
    implementation rootProject.ext.dependencies.coroutines

    kapt rootProject.ext.dependencies.hiltCompiler

    testImplementation rootProject.ext.dependencies.junit
    testImplementation rootProject.ext.dependencies.mockito

    debugImplementation rootProject.ext.dependencies.composeTestManifest
    androidTestImplementation rootProject.ext.dependencies.composeJunit
    androidTestImplementation rootProject.ext.dependencies.composeUITest
}